m4_define([intltool_minimum_version], [0.31])
m4_define([xfpm_version_major],  [0])
m4_define([xfpm_version_minor],  [6])
m4_define([xfpm_version_micro],  [0])
m4_define([xfpm_version_build],  [r@REVISION@])
m4_define([xfpm_version_tag],[svn])
m4_define([xfpm_version], [xfpm_version_major().xfpm_version_minor().xfpm_version_micro()ifelse(xfpm_version_tag(), [svn], [xfpm_version_tag().xfpm_version_build()], [xfpm_version_tag()])])

AC_INIT([xfce4-power-manager], [xfpm_version], [http://bugzilla.xfce.org/])
AC_PREREQ(2.50)

AM_INIT_AUTOMAKE([AC_PACKAGE_TARNAME()], [AC_PACKAGE_VERSION()])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()

# ============== basic compiler settings ============= #
AC_PROG_CC
AM_PROG_CC_C_O
AC_PROG_INSTALL
AC_PROG_INTLTOOL([intltool_minimum_version], [no-xml])

# ===================================================== #
# 		Check for i18n support 			#
# ===================================================== #

XDT_I18N([@LINGUAS@])


# ============== look for dependencies =============== #
# check for headers needed for standard interfaces     #
AC_HEADER_STDC
AC_CHECK_HEADERS([errno.h signal.h stddef.h \
                  string.h sys/stat.h sys/types.h sys/wait.h time.h \
                  unistd.h])

#==================================================== #
#          Check for required packages 		      #
#=====================================================#
m4_define([gtk_minimum_version], [2.9.2]) # GtkStatus icon appears from this version
m4_define([glib_minimum_version], [2.14.0]) 
m4_define([dbus_minimum_version], [0.60])
m4_define([hal_minimum_version], [0.5.6])
m4_define([xfconf_minimum_version], [4.5.90])
m4_define([libxfcegui4_minimum_version],[4.4.1])
m4_define([libxfce4util_minimum_version],[4.4.1])
m4_define([dbus_glib_minimum_version], [0.70])
m4_define([libnotify_minimum_version], [0.4.1])

XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [gtk_minimum_version])
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [glib_minimum_version])
XDT_CHECK_PACKAGE([DBUS], [dbus-1], [dbus_minimum_version])
XDT_CHECK_PACKAGE([HAL], [hal], [hal_minimum_version])
XDT_CHECK_PACKAGE([DBUS_GLIB], [dbus-glib-1], [dbus_glib_minimum_version])
XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0],[xfconf_minimum_version])
XDT_CHECK_PACKAGE([LIBXFCEGUI], [libxfcegui4-1.0],[libxfcegui4_minimum_version])
XDT_CHECK_PACKAGE([LIBXFCEUTIL], [libxfce4util-1.0],[libxfce4util_minimum_version])

#=======================================================#
#           Check for DPMS support                      #
#=======================================================#
DPMS_LIBS=
AC_ARG_ENABLE([dpms],
AC_HELP_STRING([--enable-dpms], [use dpms extension])
AC_HELP_STRING([--disable-dpms], [don't use dpms extension]),
  [], [enable_dpms=yes])
have_dpms="no"
if test x"$enable_dpms" = x"yes"; then
     AC_CHECK_LIB([Xext], [DPMSQueryExtension],
        [ have_dpms="yes"
          DPMS_LIBS=" -lXext -lX11"
          AC_DEFINE([HAVE_DPMS], [1], [Define to enable dpms])
        ],[])
fi
AC_SUBST([DPMS_LIBS])

#=======================================================#
#           Check for availability of libnotify         #
#=======================================================#
LIBNOTIFY_FOUND="no"
XDT_CHECK_OPTIONAL_PACKAGE([LIBNOTIFY],
                       [libnotify], [libnotify_minimum_version],
                       [libnotify], 
                       [libnotify library], [yes])

#=======================================================#
#              Check for debugging support              #
#=======================================================#
XDT_FEATURE_DEBUG


AC_OUTPUT([
Makefile
src/Makefile
data/Makefile
data/icons/Makefile
data/icons/scalable/Makefile
data/icons/scalable/apps/Makefile
data/icons/scalable/status/Makefile
data/icons/scalable/actions/Makefile
doc/Makefile
doc/C/Makefile
doc/C/images/Makefile
po/Makefile.in
])

echo "----------------------------------------"
echo 	 "   Build Configuration"
echo 
echo "   Debug  support          $enable_debug   "
echo "   DPMS   support          $have_dpms      "
echo "   Notification support    $LIBNOTIFY_FOUND"
echo
echo "----------------------------------------"  
echo
echo "Configuration finished, type make to compile"
